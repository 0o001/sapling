// "comment" and "whitespace" have special meaning in pest. They cause more
// trouble than benefit here. Therfore, avoid them.
// See https://pest-parser.github.io/book/grammars/syntax.html
//
// Names are used in error messages. Certain rules are used to improved UX.
// For example,
//
//    equal_sign = { space* ~ "=" ~ space* }
//    config_item = { name ~ equal_sign ~ value }
//
// is more friendly than:
//
//    config_item = { name ~ space* ~ "=" ~ space* ~ value }
//
// because the former shows "expect space", while the latter shows
// "expect equal_sign", for the following illegal content:
//
//    [section]
//    lack-of-equal-sign
//                      ^ error shows here
//
// Same applies to "directive" and "bracket"s.


new_line = { "\n" | "\r\n" }
space = { " " | "\t" }
comment_start = { ("#" | ";") }

line = @{ (!new_line ~ any)* }
name = @{ (!(space | new_line | comment_start | "=" | "[" | "]" | "%") ~ any)+ }

value = ${ line ~ (new_line ~ space+ ~ line)* }
equal_sign = @{ space* ~ "=" ~ space* }
config_item = ${ name ~ equal_sign ~ value }

left_bracket = @{ "[" }
right_bracket = @{ "]" }
section = ${ left_bracket ~ name ~ right_bracket }
comment_line = @{ comment_start ~ line }
blank_line = @{ space* }

directive = ${ "%" ~ (include | unset) }
include = ${ "include" ~ space+ ~ line  }
unset = ${ "unset" ~ space+ ~ name ~ space* }

compound = _{ (config_item | section | comment_line | directive | blank_line) }
file = _{ soi ~ compound ~ (new_line ~ compound)* ~ eoi }
